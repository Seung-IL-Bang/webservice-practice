buildscript { // 프로젝트의 플러그인 의존성 관리를 위한 설정이다.
    ext { // ext: build.gradle 에서 사용하는 전역 변수를 설정하겠단 키워드이다.
        springBootVersion = '2.1.9.RELEASE' // springBootVersion 전역 변수를 생성하고 그 값을 2.1.9.RELEASE 로 설정
    }
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies { // 이렇게 버전 관리를 할 경우 각 라이브러리들의 버전 관리가 한 곳에 집중되고, 버전 충돌 문제도 해결되어 편하게 개발이 가능하다.
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

// 아래는 buildscript 에서 선언한 플러그인 의존성들을 적용할 것인지를 결정하는 코드이다.
// 아래 4개 플러그인들은 자바와 스프링 부트를 사용하기 위해서는 필수 플러그인들이니 항상 추가해주면 된다.
apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management' // 스프링 부트의 의존성들을 관리해 주는 플러그인이라 꼭 추가해야 한다.

group 'org.example'
version '1.0-SNAPSHOT'

repositories { // repositories 는 각종 의존성(라이브러리)들을 어떤 원격 저장소에서 받을지를 정한다.
    mavenCentral() // 기본적으로 mavenCentral 을 많이 사용한다.
    jcenter() // 최근에는 라이브러리 업로드 난이도 때문에 jcenter 도 많이 사용한다.
}

dependencies { // 프로젝트 개발에 필요한 의존성들을 선언하는 곳이다.
    implementation('org.springframework.boot:spring-boot-starter-web') // 버전을 명시하지 않아야 맨 위에 작성한 springbootVersion 을 따라간다.
    implementation('org.springframework.boot:spring-boot-starter-test')
    implementation('org.springframework.boot:spring-boot-starter-data-jpa') // 스프링 부트용  Spring Data Jpa 추상화 라이브러리 이다. 스프링 부트 버전에 맞춰 자동으로 JPA 관련 라이브러리들의 버전을 관리해 준다.
    implementation('com.h2database:h2') // 인메모리 관계형 데이터베이스이다. 별도의 설치 없이 의존성만으로 관리 할 수 있다. 메모리에서 실행되기 때문에 애플리케이션을 재시작할 때마다 초기화 된다는 점을 이용하여 로컬 테스트용으로 많이 사용한다.
    implementation('org.springframework.boot:spring-boot-starter-mustache') // 템플릿 엔진 머스테치 의존성 주입.
    implementation('org.springframework.boot:spring-boot-starter-oauth2-client') // 소셜 로그인 등 클라이언트 입장에서 소셜 기능 구현 시 필요한 의존성이다. spring-security-oauth2-client와 spring-security-oauth2-jose를 기본으로 관리해준다.
    implementation('org.springframework.session:spring-session-jdbc') // 세션 저장소를 jdbc로 할때 필요한 의존성

    compileOnly('org.projectlombok:lombok')
    annotationProcessor('org.projectlombok:lombok')

}
